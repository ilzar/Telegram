apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:support-v4:23.0.1'
    compile 'com.google.android.gms:play-services:3.2.+'
    compile 'net.hockeyapp.android:HockeySDK:3.5.0'
    compile 'com.googlecode.mp4parser:isoparser:1.0.6'
    compile 'org.apache.httpcomponents:httpmime:4.2.1'
    compile 'com.github.bumptech.glide:glide:3.6.1'
}

android {

    compileSdkVersion 22
    buildToolsVersion '23.0.1'

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        debug {
            storeFile file("config/debug.keystore")
        }

        release {
            storeFile file("config/release.keystore")
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.debug
            applicationIdSuffix ".dev"
        }

        release {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.release
            applicationIdSuffix ".fenegram"
        }

        foss {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets.main {
        jniLibs.srcDir 'libs'
        jni.srcDirs = [] //disable automatic ndk-build call
    }

    sourceSets.debug {
        manifest.srcFile 'config/debug/AndroidManifest.xml'
    }

    sourceSets.release {
        manifest.srcFile 'config/release/AndroidManifest.xml'
    }

    sourceSets.foss {
        manifest.srcFile 'config/foss/AndroidManifest.xml'
    }

    defaultConfig {
        minSdkVersion 14
        //noinspection OldTargetApi
        targetSdkVersion 22
        versionCode 627
        versionName "3.2.2"

        buildConfigField "int", "APP_ID", "${property('app.id')}"
        buildConfigField "String", "APP_HASH", "\"${property('app.hash')}\""
        buildConfigField "String", "LOG_EMAIL", "\"${property('log.email')}\""
        buildConfigField "String", "HOCKEYAPP_HASH", "\"${property('hockeyapp.hash')}\""
    }
}
